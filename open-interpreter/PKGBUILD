# Maintainer: Your Name <your@email.com>

pkgname="open-interpreter"
pkgver=0.4.2
pkgrel=1
pkgdesc="An open-source implementation of OpenAI's Code Interpreter."
url="https://github.com/KillianLucas/open-interpreter"
arch=("any")
license=("MIT")
provides=("open-interpreter")
conflicts=("open-interpreter-git" "python-open-interpreter")
replaces=("python-open-interpreter")
depends=("python")
makedepends=("python-build"
             "python-wheel"
             "python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/o/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('0499109ba3a63d755efb6f379c98ea69f0b71c3c2ec3770b9829dfc65ff03d29')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    python -m build --no-isolation --wheel
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    # Create virtual environment
    export PIP_DISABLE_PIP_VERSION_CHECK=1
    export PYTHONDONTWRITEBYTECODE=1
    python -m venv "$pkgdir/usr/share/open-interpreter/venv"
    source "$pkgdir/usr/share/open-interpreter/venv/bin/activate"

    # Install open-interpreter and its dependencies using pip
    "$pkgdir/usr/share/open-interpreter/venv/bin/"pip install dist/*.whl

    # Remove references to pkgdir
    find "$pkgdir/usr/share/open-interpreter/venv/bin" -maxdepth 1 -type f -exec sed -i "s|${pkgdir}/|/|g" {} +
    find "$pkgdir/usr/share/open-interpreter/venv/pyvenv.cfg" -maxdepth 1 -type f -exec sed -i "s|${pkgdir}/|/|g" {} +
    find "$pkgdir/usr/share/open-interpreter/venv" -type f -name "*.py[co]" -delete
    find "$pkgdir/usr/share/open-interpreter/venv" -type d -name "__pycache__" -delete

    # Exit virtual environment
    deactivate

    # Create executable symlink
    install -d "$pkgdir/usr/bin"
    ln -s "/usr/share/open-interpreter/venv/bin/interpreter" "$pkgdir/usr/bin/interpreter"
}
